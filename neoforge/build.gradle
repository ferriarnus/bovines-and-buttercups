import org.apache.tools.ant.filters.LineContains

plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.45'
    id 'net.neoforged.gradle.mixin' version '7.0.45'
    id 'java-library'
}
base {
    archivesName = "${mod_id}-neoforge"
}

mixin {
    config("${mod_id}.mixins.json")
    config("${mod_id}.neoforge.mixins.json")
}

jarJar.enable()

if (file('src/main/resources/META-INF/accesstransformer.cfg').exists()) {
    minecraft.accessTransformers.file file('src/main/resources/META-INF/accesstransformer.cfg')
}

runs {
    configureEach {
        modSource project.sourceSets.main
    }
    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }
    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }
    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
    maven {
        url 'https://maven.willbl.dev/releases/'
    }
    mavenLocal()
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"

    compileOnly project(":common")

    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")

    implementation(jarJar("com.williambl.dfunc:data-functions-neoforge:${dfunc_version}")) {
        jarJar.ranged(it, "[${dfunc_version},)")
    }
    implementation(jarJar("io.github.llamalad7:mixinextras-neoforge:${mixin_extras_version}")) {
        jarJar.ranged(it, "[${mixin_extras_version},)")
    }

    compileOnly group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
    annotationProcessor group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
}

// NeoGradle compiles the game, but we don't want to add our common code to the game's code
Spec<Task> notNeoTask = { Task it -> !it.name.startsWith("neo") } as Spec<Task>

tasks.withType(JavaCompile).matching(notNeoTask).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

tasks.withType(Javadoc).matching(notNeoTask).configureEach {
    source(project(":common").sourceSets.main.allJava)
}

tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

tasks.withType(ProcessResources).matching(notNeoTask).configureEach {
    from project(":common").sourceSets.main.resources

    filesMatching("*.mixins.json") {
        filter(LineContains, negate: true, contains: [ 'refmap' ] )
    }
}

tasks.named('jarJar').configure {
    archiveClassifier = ""
}

tasks.named('build').configure {
    dependsOn('jarJar')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
